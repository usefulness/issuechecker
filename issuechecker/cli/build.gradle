plugins {
    alias(libs.plugins.starter.library.kotlin)
    id "application"
    id "maven-publish"
    alias(libs.plugins.johnrengelman.shadow)
}

application {
    mainClass.set("com.project.starter.issuechecker.cli.Main")
}

tasks.named("test") {
    useJUnitPlatform()
    testLogging {
        events "skipped", "failed"
    }
}

configurations {
    r8
}

dependencies {
    implementation(project(":issuechecker"))
    implementation(libs.clikt.core)
    constraints {
        implementation(libs.mordant.core) {
            because 'only the latest version published proper R8 rules'
        }
    }

    r8(libs.r8.core)

    testRuntimeOnly(libs.junit.platform.launcher)

    testImplementation(libs.assertj.core)
    testImplementation(libs.junit.jupiter.api)
}

tasks.named("shadowJar") {
    exclude '**/*.kotlin_metadata'
    exclude '**/*.kotlin_module'
    exclude '**/*.kotlin_builtins'
    exclude '**/module-info.class'
    exclude 'META-INF/proguard/**'
    exclude 'META-INF/com.android.tools/proguard/**'
    exclude 'META-INF/com.android.tools/r8/**'
    exclude 'META-INF/*.version'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/AL2.0'
    exclude 'META-INF/LGPL2.1'
    exclude '**/*.proto'
    exclude '**/*.dex'
    exclude 'LICENSE'
    exclude 'NOTICE'
    exclude 'r8-version.properties'
    exclude 'migrateToAndroidx/*'
    exclude 'DebugProbesKt.bin'
}

def r8File = layout.buildDirectory.file("libs/${base.archivesName.get()}-$version-r8.jar").get().asFile
def rulesFile = project.file("rules.pro")
def r8JarProvider = tasks.register('r8Jar', JavaExec) { task ->
    task.javaLauncher.set(javaToolchains.launcherFor { spec ->
        spec.languageVersion.set(JavaLanguageVersion.of(JavaVersion.VERSION_21.majorVersion))
    })
    def fatJar = shadowJar
    def fatJarFile = fatJar.archiveFile
    task.dependsOn(fatJar)
    task.inputs.file(fatJarFile)
    task.inputs.file(rulesFile)
    task.outputs.file(r8File)

    task.classpath(configurations.r8)
    task.mainClass.set('com.android.tools.r8.R8')
    task.args = [
            '--release',
            '--classfile',
            '--output', r8File.path,
            '--pg-conf', rulesFile.path,
            '--lib', System.getProperty('java.home').toString(),
            fatJarFile.get().asFile.path,
    ]
}
artifacts {
    archives file: r8File, name: 'binary', type: 'jar', builtBy: r8JarProvider, classifier: 'diffuse'
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            artifact r8JarProvider
        }
    }
    repositories {
        maven {
            name "github"
            url "https://maven.pkg.github.com/usefulness/issuechecker"
            credentials {
                username = "usefulness"
                password = findProperty("GITHUB_TOKEN")?.toString() ?: System.getenv("GITHUB_TOKEN") ?: ""
            }
        }
    }
}
