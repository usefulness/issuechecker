import org.gradle.api.initialization.resolve.RepositoriesMode

pluginManagement {
    repositories { // https://github.com/gradle/gradle/issues/20866
        gradlePluginPortal()
        mavenCentral()
        google()
    }
}

plugins {
    id("org.gradle.toolchains.foojay-resolver-convention") version "0.8.0"
    id("com.gradle.develocity") version "3.17.4"
}

develocity {
    buildScan {
        termsOfUseUrl = "https://gradle.com/terms-of-service"
        termsOfUseAgree = "yes"

        uploadInBackground = System.getenv("CI") == null
        publishing { onlyIf { System.getenv("CI") != null } }
    }
}

dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.name = 'com.github.usefulness'

includeBuild("gradle/plugins")
include "issuechecker", "issuechecker:cli"
