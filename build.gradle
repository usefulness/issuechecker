import org.jetbrains.kotlin.gradle.dsl.KotlinVersion
import org.jetbrains.kotlin.gradle.tasks.KotlinJvmCompile

plugins {
    alias(libs.plugins.starter.config)
    alias(libs.plugins.starter.versioning)
    alias(libs.plugins.starter.library.kotlin) apply(false)
    alias(libs.plugins.kotlinx.binarycompatibility)
}

commonConfig {
    javaFilesAllowed false
    javaVersion JavaVersion.VERSION_1_8
}

allprojects {
    pluginManager.withPlugin("kotlin") { plugin ->
        configurations.named { it != "detekt" }.configureEach {
            resolutionStrategy.eachDependency {
                if (requested.group == 'org.jetbrains.kotlin' && requested.name.startsWith("kotlin")) {
                    useVersion libs.versions.mavencentral.kotlin.get()
                }
            }
        }
        kotlin {
            jvmToolchain(24)
        }

        tasks.withType(KotlinJvmCompile).configureEach {
            compilerOptions {
                apiVersion = KotlinVersion.KOTLIN_2_0
                languageVersion = KotlinVersion.KOTLIN_2_0
            }
        }
    }
}
