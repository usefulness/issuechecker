import org.jetbrains.kotlin.gradle.plugin.KotlinPluginWrapperKt
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    alias(libs.plugins.starter.config)
    alias(libs.plugins.starter.versioning)
    alias(libs.plugins.osacky.gradledoctor)
    alias(libs.plugins.starter.library.kotlin) apply(false)
    alias(libs.plugins.kotlinx.binarycompatibility)
}

commonConfig {
    javaFilesAllowed false
    javaVersion JavaVersion.VERSION_1_8
}

allprojects {
    pluginManager.withPlugin("kotlin") { plugin ->
        def kotlinVersion = KotlinPluginWrapperKt.getKotlinPluginVersion(project)
        configurations.configureEach {
            resolutionStrategy.eachDependency {
                if (requested.group == 'org.jetbrains.kotlin' && requested.name.startsWith("kotlin")) {
                    useVersion kotlinVersion
                }
            }
        }
        kotlin {
            jvmToolchain(20)
        }
    }
    pluginManager.withPlugin("jacoco") {
        jacoco {
            toolVersion = "0.8.10"
        }
    }
    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions {
            apiVersion = "1.5"
            freeCompilerArgs += "-opt-in=kotlin.time.ExperimentalTime"
        }
    }
}
